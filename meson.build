project('mimic-indic', 'c', default_options : ['c_std=c99'], version: '0')

build_plugin = get_option('plugin')

sources = [
  'src/cmu_indic_lex/cmu_indic_lex.c',
  'src/cmu_indic_lang/cmu_indic_lang.c',
  'src/cmu_indic_lang/cmu_indic_phoneset.c',
  'src/cmu_indic_lang/cmu_indic_phrasing_cart.c'
]

includes = [include_directories('include')]

# Core Dependency:
mimiccore = dependency('ttsmimic_core', required: false)
if mimiccore.found()
  mimic_plugin_dir = mimiccore.get_pkgconfig_variable('mimic_plugin_dir')
else
  # We compile it:
  mimic_core_sub = subproject('mimic-core')
  mimiccore = mimic_core_sub.get_variable('ttsmimic_core_dep')
  mimic_plugin_dir = mimic_core_sub.get_variable('mimic_plugin_dir')
endif

# If mimic-core does not have plugin support, disable the plugin
if mimic_plugin_dir == ''
    build_plugin = false
endif

# English Dependency (for the lexicon)
mimic_english = dependency('ttsmimic_english', required: false)
if not mimic_english.found()
  # We compile it:
  mimic_english_sub = subproject('mimic-english')
  mimic_english = mimic_english_sub.get_variable('ttsmimic_english_dep')
  includes += mimic_english_sub.get_variable('includes')
endif


message('Build mimic-indic plugin: @0@'.format(build_plugin))


indic_header_subdir = join_paths('ttsmimic', 'lang', 'indic')

conf_data = configuration_data()
conf_data.set('ENABLE_LANG_INDIC_BUILTIN', not build_plugin)

mimic_indic_config_h = configure_file(output : 'mimic_indic_config.h',
                                        configuration : conf_data, install: true,
                                        install_dir : join_paths(get_option('includedir'), indic_header_subdir))
sources += [mimic_indic_config_h]

ttsmimic_indic_lib = library('ttsmimic_indic', sources,
                             include_directories : includes,
                             dependencies: [mimiccore, mimic_english],
                             install: true)

ttsmimiclib_indic_dep = declare_dependency(link_with : ttsmimic_indic_lib, 
                                           include_directories : includes)


if build_plugin
  ttsmimic_indic_plugin_lib = library(
    'ttsmimic_indic_plugin',
    ['src/mimic_indic_plugin.c'],
    include_directories : includes,
    dependencies: [mimiccore, mimic_english, ttsmimiclib_indic_dep],
    install_dir: mimic_plugin_dir,
    install: true)

  ttsmimiclib_indic_plugin_dep = declare_dependency(
    link_with : ttsmimic_indic_plugin_lib, 
    include_directories : includes)
endif



############ Headers #################

ttsmimic_headers = ['include/cmu_indic_lang.h', 'include/cmu_indic_lex.h']

install_headers(ttsmimic_headers, subdir : indic_header_subdir)

# FIXME: Some files were autogenerated from festival sources.
# It would be cool to regenerate them from sources easily.
# At least the regex in cmu_indic_lang.c

########### pkg-config #############

pkg = import('pkgconfig')

pkg.generate(libraries : ttsmimic_indic_lib,
             name : 'ttsmimic_indic',
             subdirs: indic_header_subdir,
             requires: ['ttsmimic_core', 'ttsmimic_english'],
             description: 'Indic support for text to speech synthesis in mimic.',
             version: meson.project_version())

